---
title: Erlang
slug: /note/7-languages-in-7-weeks/erlang
date: 2020-03-30
description: 《7 周 7 语言》读书笔记
tags:
  - Note
  - 7 Languages in 7 Weeks
---

无线程，轻量级进程：摆脱了共享资源和性能瓶颈

可靠性：Let it crash、hot reload 可以运行多年不宕机

小写是原子，大写开头是变量

```erlang
two == two.
% true
Var = 1.
```

变量不可变，只能被赋值一次

列表与元组

```erlang
[1, two, "three"].
{1, two, "three"}.
```

模式匹配

```erlang
Person = {ahabhgk, 19}.
{Name, Age} = Person.

[One, Two | Rest] = [1, 2, 3].
One. % 1
Two. % 2
Rest. % [3]
```

位匹配

函数

```erlang
% basic.erl
-module(basic). % 模块名
-export([number/1]). % 暴露出的函数，/1 表示有一个参数
-export([fib/1]).

number(one) -> 1;
number(two) -> 2;
number(three) -> 3.

fib(0) -> 1;
fib(1) -> 1;
fib(N) -> fib(N - 1) + fib(N - 2).
```

```shell
$ erl
> c(basic).
{ok,basic}
> basic:fib(10).
89
```

case、if

```erlang
Animal = "dog"
case Animal of
  "cat" -> thundercat;
  "elephant" -> dumbo
  _ -> something_else
end.

X = 0
if
  X > 0 -> positive;
  X < 0 -> negative;
  true -> zero
end.
```

匿名函数

```erlang
Negate = fun(I) -> -I end.
Negate(1). % -1
```

高阶函数

foreach map all any takewith dropwith foldl

```erlang
Numbers = [1, 2, 3, 4].
lists:map(fun(Number) -> Number + 1 end, Numbers).

% map(F, [Head | Tail]) -> [F(Head) | map(F, Tail)];
% map(F, []) -> [].

Adder = fun(ListItem, SumSoFor) -> ListItem + SumSoFor end.
lists:foldl(Adder, 0, Numbers).
```

列表构造

```erlang
[1 | [2, 3]]. % [1, 2, 3]
```

列表解析

```erlang
lists:map(Double, Numbers).
% 等价于
[Double(X) || X <- Numbers].
% 等价于
[X * 2 || X <- [1, 2, 3, 4]].

% map 定义
% map(F, L) -> [F(X) || X <- L].

% 可以有多个子句，子句可以是 filter 或 generator（Match <- List）
[{X, Y} || X <- [1, 2, 3, 4], X < 3, Y <- [5, 6]].
% [{1, 5}, {1, 6}, {2, 5}, {2, 6}].
```

## 并发

```erlang
-module(translate).
-export([loop/0]).

loop() ->
  receive % receive 用来接收其他进程发过来的消息
    "casa" ->
      io:format("house~n"),
      loop();
    "blanca" ->
      io:format("white~n"),
      loop();
    _ ->
      io:format("I dont understand.~n"),
      loop()
end.
```

```shell
$ erl
> c(translate).
> Pid = spawn(fun translate:loop/0). % 通过 spawn 启动进程
# 可以通过其他形式定义，比如不用 Pid 而用名字注册进程
# 还有支持 hot reload 的形式
# 远程进程 spawn(Node, function).
> Pid ! "casa".
house
"casa"
> Pid ! "haa".
I dont understand.
"haa"
# 分布式消息发送给命名资源 node@server ! message
```

同步消息

```erlang
-module(translate_service).
-export([loop/0, translate/2]).

loop() ->
  receive
    {From, "casa"} ->
      From ! "house",
      loop();

    {From, "blanca"} ->
      From ! "white",
      loop();

    {From, _} ->
      From ! "I do not understand.",
      loop()
  end.

translate(To, Word) ->
  To ! {self(), Word},
  receive
    Translation -> Translation
  end.
```

```shell
> Translator = spawn(fun translate_service:loop/0).
> translate_service:translate(Translator, "blanca").
"white"
> translate_service:translate(Translator, "casa").
"house"
```

链接进程以获得可靠性

```erlang
% roulette.erl
-module(roulette).
-export([loop/0]).

loop() ->
  receive
    3 -> io:format("bang.~n"), exit({roulette, die, at, erlang:time()});
    _ -> io:format("click~n"), loop()
  end.
```

```erlang
% doctor.erl
-module(doctor).
-export([loop/0]).

loop() ->
  process_flag(trap_exit, true),
  receive
    new ->
      io:format("creating and monitoring process.~n"),
      register(revolver, spawn_link(fun roulette:loop/0)),
       loop();
 
    {'EXIT', From, Reason} ->
      io:format("the shooter ~p died with reason ~p.", [From, Reason]),
      io:format("restarting.~n"),
      self() ! new,
      loop()
  end.
```

```shell
$ erl
> c(doctor).
> c(roulette).
> Doc = spawn(fun doctor:loop/0).
<0.86.0>
> Doc ! new.
creating and monitoring process.
> revolver ! 1.
click
1
> revolver ! 3.
bang.
3
the shooter <0.88.0> died with reason {roulette,die,at,{21,43,19}}.restarting.
creating and monitoring process.
> revolver ! 4.
click
4
```

## feeling

动态

可靠性

轻量级进程
